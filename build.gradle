buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.1.+', changing: true
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

group = 'com.lovetropics.donations'
archivesBaseName = 'LTDonations'

ext.buildnumber = 0
project.buildnumber = System.getenv('BUILD_NUMBER') ?: 'custom'
version = "${mod_version}-${release_type}+${buildnumber}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: 'official', version: minecraft_version
    
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
        	taskName 'ltdonations_client'
        	
            workingDirectory project.file('run')

            property 'forge.logging.console.level', 'info'

            mods {
                ltdonations {
                    source sourceSets.main
                }
            }
        }

        server {
        	taskName 'ltdonations_server'
        
            workingDirectory project.file('run')

            property 'forge.logging.console.level', 'info'

            mods {
                ltdonations {
                    source sourceSets.main
                }
            }
        }
        
        data {
        	taskName 'ltdonations_data'
        
            workingDirectory project.file('run')
            
            environment 'target', 'fmluserdevdata'

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                ltdonations {
                    source sourceSets.main
                }
            }
            
			args '--mod', 'ltdonations', '--all', '--output', '"' + rootProject.file('src/generated/resources/') + '"',
				 '--existing', '"' + sourceSets.main.resources.srcDirs[0] + '"' 
			
        }
    }
}

sourceSets.main.resources {
	srcDir 'src/generated/resources'
}

repositories {
	mavenLocal()
}

configurations {
    shade
    shadelib
    implementation.extendsFrom shadelib
}

repositories {
	maven { // Registrate
		url "http://maven.tterrag.com/"
	}
	mavenLocal()
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    
	def registrate = "com.tterrag.registrate:Registrate:MC1.16.2-${registrate_version}"
    implementation fg.deobf(registrate)
    shade registrate
    
    def ltlib = "com.lovetropics.lib:LTLib:${ltlib_version}"
    implementation fg.deobf(ltlib)
    shade ltlib
}

shadowJar {
    configurations = [project.configurations.shade, project.configurations.shadelib]
    relocate 'com.tterrag.registrate', 'com.lovetropics.donations.repack.registrate'
    relocate 'com.lovetropics.lib', 'com.lovetropics.donations.repack.ltlib'
    classifier = ''
}

reobf {
    shadowJar {}
}

build.dependsOn shadowJar
build.dependsOn reobfShadowJar

jar {
    classifier = 'slim'

    manifest {
        attributes([
            "Specification-Title": "ltdonations",
            "Specification-Vendor": "lovetropics",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"lovetropics",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
